CREATE OR REPLACE PACKAGE INGRESOS_PKG AS

    PROCEDURE SP_REGISTRAR_INGRESO(
        P_SUCURSAL_ID    IN NUMBER,
        P_DESCRIPCION    IN VARCHAR2,
        P_MONTO          IN NUMBER,
        P_FECHA_RECIBIDO IN DATE,
        P_REGISTRADO_POR IN NUMBER,
        P_TIPO           IN VARCHAR2
    );

    PROCEDURE SP_INGRESO_LEER_POR_FECHA(
        P_FECHA_INICIO IN DATE,
        P_FECHA_FIN    IN DATE,
        P_INGRESOS     OUT SYS_REFCURSOR
    );
    
    FUNCTION FN_SUMA_TOTAL_POR_FECHA(
        P_FECHA_INICIO IN DATE,
        P_FECHA_FIN    IN DATE
    ) RETURN NUMBER;

END INGRESOS_PKG;
/
CREATE OR REPLACE PACKAGE BODY INGRESOS_PKG AS

    FUNCTION FN_SUMA_TOTAL_POR_FECHA(
        P_FECHA_INICIO IN DATE,
        P_FECHA_FIN    IN DATE
    ) RETURN NUMBER
    AS
        V_SUMA NUMBER := 0;
    BEGIN
        SELECT NVL(SUM(MONTO), 0)
        INTO V_SUMA
        FROM INGRESOS
        WHERE FECHA_RECIBIDO BETWEEN P_FECHA_INICIO AND P_FECHA_FIN;
        
        RETURN V_SUMA;
    END FN_SUMA_TOTAL_POR_FECHA;


    PROCEDURE SP_REGISTRAR_INGRESO(
        P_SUCURSAL_ID    IN NUMBER,
        P_DESCRIPCION    IN VARCHAR2,
        P_MONTO          IN NUMBER,
        P_FECHA_RECIBIDO IN DATE,
        P_REGISTRADO_POR IN NUMBER,
        P_TIPO           IN VARCHAR2
    ) AS
    BEGIN
        -- Validar que el monto sea positivo
        IF P_MONTO <= 0 THEN
            RAISE_APPLICATION_ERROR(-20201, 'El monto del ingreso debe ser positivo.');
        END IF;

        INSERT INTO INGRESOS (SUCURSAL_ID, DESCRIPCION, MONTO, FECHA_RECIBIDO, REGISTRADO_POR, TIPO)
        VALUES (P_SUCURSAL_ID, P_DESCRIPCION, P_MONTO, P_FECHA_RECIBIDO, P_REGISTRADO_POR, P_TIPO);

    EXCEPTION
        WHEN OTHERS THEN
            RAISE_APPLICATION_ERROR(-20202, 'Error al registrar el ingreso: ' || SQLERRM);
    END SP_REGISTRAR_INGRESO;


    PROCEDURE SP_INGRESO_LEER_POR_FECHA(
        P_FECHA_INICIO IN DATE,
        P_FECHA_FIN    IN DATE,
        P_INGRESOS     OUT SYS_REFCURSOR
    ) AS
    BEGIN
        OPEN P_INGRESOS FOR
            SELECT ID_INGRESOS, SUCURSAL_ID, DESCRIPCION, MONTO, FECHA_RECIBIDO, TIPO
            FROM INGRESOS
            WHERE FECHA_RECIBIDO BETWEEN P_FECHA_INICIO AND P_FECHA_FIN
            ORDER BY FECHA_RECIBIDO DESC;

    EXCEPTION
        WHEN OTHERS THEN
            RAISE_APPLICATION_ERROR(-20203, 'Error al leer ingresos por fecha: ' || SQLERRM);
    END SP_INGRESO_LEER_POR_FECHA;

END INGRESOS_PKG;
/

CREATE OR REPLACE PACKAGE FACTURAS_PKG AS

    PROCEDURE SP_FACTURA_CREAR(
        P_USUARIO_ID IN NUMBER,
        P_ORDEN_ID IN NUMBER,
        P_MONTO_TOTAL IN NUMBER,
        P_DESCUENTOS IN NUMBER,
        P_CUPON_ID IN NUMBER,
        P_CANAL_ID IN NUMBER,
        P_SUCURSAL_ID IN NUMBER,
        P_ESTADO IN VARCHAR2 DEFAULT 'PENDIENTE',
        P_ID_FACTURA OUT NUMBER
    );

    PROCEDURE SP_FACTURA_LEER_POR_FECHA(
        P_FECHA_INICIO IN DATE,
        P_FECHA_FIN IN DATE,
        P_FACTURAS OUT SYS_REFCURSOR
    );
    
    PROCEDURE SP_FACTURA_ACTUALIZAR_ESTADO(
        P_FACTURA_ID IN NUMBER,
        P_NUEVO_ESTADO IN VARCHAR2
    );

END FACTURAS_PKG;
/
CREATE OR REPLACE PACKAGE BODY FACTURAS_PKG AS

    PROCEDURE SP_FACTURA_CREAR(
        P_USUARIO_ID   IN NUMBER,
        P_ORDEN_ID     IN NUMBER,
        P_MONTO_TOTAL  IN NUMBER,
        P_DESCUENTOS   IN NUMBER,
        P_CUPON_ID     IN NUMBER,
        P_CANAL_ID     IN NUMBER,
        P_SUCURSAL_ID  IN NUMBER,
        P_ESTADO       IN VARCHAR2,
        P_ID_FACTURA   OUT NUMBER
    ) AS
    BEGIN
        IF P_MONTO_TOTAL <= 0 THEN
             RAISE_APPLICATION_ERROR(-20220, 'El monto total (TOTAL) de la factura debe ser positivo.');
        END IF;
        
        INSERT INTO FACTURAS (
            CUPON_ID, CANAL_ID, SUCURSAL_ID, ORDEN_ID, TOTAL, DESCUENTOS, FECHA_EMISION, ESTADO, USUARIO_ID
        )
        VALUES (
            P_CUPON_ID, P_CANAL_ID, P_SUCURSAL_ID, P_ORDEN_ID, P_MONTO_TOTAL, P_DESCUENTOS, SYSDATE, P_ESTADO, P_USUARIO_ID
        )
        RETURNING ID_FACTURA INTO P_ID_FACTURA; 

    EXCEPTION
        WHEN OTHERS THEN
            RAISE_APPLICATION_ERROR(-20221, 'Error al crear la factura: ' || SQLERRM);
    END SP_FACTURA_CREAR;


    PROCEDURE SP_FACTURA_LEER_POR_FECHA(
        P_FECHA_INICIO IN DATE,
        P_FECHA_FIN    IN DATE,
        P_FACTURAS     OUT SYS_REFCURSOR
    ) AS
    BEGIN
        OPEN P_FACTURAS FOR
            SELECT 
                ID_FACTURA,
                NUMERO_FACTURA,
                CUPON_ID,
                CANAL_ID,
                USUARIO_ID,
                SUCURSAL_ID,
                ORDEN_ID,
                TOTAL,
                DESCUENTOS,
                FECHA_EMISION
            FROM FACTURAS
            WHERE FECHA_EMISION BETWEEN P_FECHA_INICIO AND P_FECHA_FIN
            ORDER BY FECHA_EMISION DESC;

    EXCEPTION
        WHEN OTHERS THEN
            RAISE_APPLICATION_ERROR(-20222, 'Error al leer facturas por fecha: ' || SQLERRM);
    END SP_FACTURA_LEER_POR_FECHA;


    PROCEDURE SP_FACTURA_ACTUALIZAR_ESTADO(
        P_FACTURA_ID IN NUMBER,
        P_NUEVO_ESTADO IN VARCHAR2
    ) AS
    BEGIN
        UPDATE FACTURAS
        SET ESTADO = P_NUEVO_ESTADO
        WHERE ID_FACTURA = P_FACTURA_ID;
        
        IF SQL%ROWCOUNT = 0 THEN
            RAISE_APPLICATION_ERROR(-20223, 'No se encontrÃ³ la factura ID: ' || P_FACTURA_ID || ' para actualizar el estado.');
        END IF;

    EXCEPTION
        WHEN OTHERS THEN
            RAISE_APPLICATION_ERROR(-20224, 'Error al actualizar el estado de la factura: ' || SQLERRM);
    END SP_FACTURA_ACTUALIZAR_ESTADO;

END FACTURAS_PKG;
/